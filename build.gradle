plugins {
  id 'com.github.ben-manes.versions' version '0.53.0'
  id 'scala'
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'com.gradle.plugin-publish' version '2.0.0'
  id 'com.gradleup.nmcp.aggregation' version '1.2.0'
}

description = 'Deploy Service to Google Cloud Run'
group = 'org.podval.tools'
version = '0.4.2'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

scala.scalaVersion = '3.7.4'

dependencies {
  implementation 'com.google.auth:google-auth-library-oauth2-http:1.40.0'
  implementation 'com.google.api-client:google-api-client:2.8.1'
  implementation 'org.slf4j:slf4j-api:2.0.17'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1'

  // see https://cloud.google.com/run/docs/reference/about-apis -
  // I am making API calls, but I rely on Kubernetes compatibility, so I am staying with v1.
  implementation 'com.google.apis:google-api-services-run:v1-rev20251031-2.0.0'

  // JIB Gradle plugin versions after 3.2.1 are available as Maven artifacts only
  // - in the Gradle Plugin Portal Maven repository and
  // - without the `gradle.plugin.` prefix on the group
  // (see https://github.com/GoogleContainerTools/jib/blob/master/jib-gradle-plugin/build.gradle#L10):
  compileOnly    'com.google.cloud.tools:jib-gradle-plugin:3.4.5'
}

// There is no Java in the project :)
project.gradle.startParameter.excludedTaskNames.add('compileJava')
tasks.withType(ScalaCompile).configureEach {
  scalaCompileOptions.with {
    targetCompatibility = '' // TODO remove when https://youtrack.jetbrains.com/issue/SCL-18813 is fixed
    additionalParameters = [
      '-new-syntax',
      '-feature',
      '-language:strictEquality',
      '-source:future'
    ]
  }
}

final String gitHubRepository = "dubinsky/cloud-run"
final String gitHubRepositoryUrl = "https://github.com/$gitHubRepository"
final String orgName = 'Podval Group'
final String orgUrl = 'https://www.podval.org'

jar {
  manifest {
    attributes(
      'Implementation-Title'  : project.description,
      'Implementation-Version': project.version
    )
  }
}

tasks.register('sourcesJar', Jar) {
  from sourceSets.main.allSource
  archiveClassifier.set('sources')
}

tasks.register('scaladocJar', Jar) {
  from scaladoc.destinationDir
  archiveClassifier.set('scaladoc')
}
scaladocJar.dependsOn scaladoc

// There is no Java in the project :)
project.gradle.startParameter.excludedTaskNames.add('compileJava')
// but Maven Central requires javadoc JAR...
tasks.register('javadocJar', Jar) {
  from javadoc.destinationDir
  archiveClassifier.set('javadoc')
}
javadocJar.dependsOn(javadoc)

// ./gradlew nmcpZipAggregation - to see what will be published
// ./gradlew publishAggregationToCentralPortal - to publish
// https://central.sonatype.com/publishing/deployments - to release
nmcpAggregation {
  // Publish all projects that apply the 'maven-publish' plugin
  publishAllProjectsProbablyBreakingProjectIsolation()

  centralPortal {
    publishingType = "USER_MANAGED"
    username = findProperty('mavenCentralUsername') as String
    password = findProperty('mavenCentralPassword') as String
  }
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact scaladocJar
      artifact javadocJar

      pom {
        name = project.name
        description = project.description
        url = gitHubRepositoryUrl
        scm {
          url = gitHubRepositoryUrl
          connection = "scm:git:git://github.com/${gitHubRepository}.git"
          developerConnection = "scm:git:ssh://github.com/${gitHubRepository}.git"
        }
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
            comments = 'A business-friendly OSS license'
          }
        }
        organization {
          name = orgName
          url = orgUrl
        }
        developers {
          developer {
            id = 'dub'
            name = 'Leonid Dubinsky'
            email = 'dub@podval.org'
            url = 'https://dub.podval.org'
            organization = orgName
            organizationUrl = orgUrl
            timezone = '-5'
          }
        }
      }
    }
  }
}

signing {
  useInMemoryPgpKeys(
    findProperty('signingKey') as String,
    findProperty('signingPassword') as String
  )
  sign publishing.publications.library
}

gradlePlugin {
  website = gitHubRepositoryUrl
  vcsUrl  = gitHubRepositoryUrl //.git' ?
  plugins {
    cloudRun {
      id = project.name
      implementationClass = 'org.podval.tools.cloudrun.CloudRunPlugin'
      displayName = project.description
      description = project.description
      tags.set(['Google', 'CloudRun', 'JIB', 'Docker'])
    }
  }
}

tasks.register('uploadLibrary')
uploadLibrary.description = 'Upload artifacts'
uploadLibrary.group = 'publishing'
uploadLibrary.dependsOn(publishAggregationToCentralPortal)

tasks.register('upload')
upload.description = 'Upload artifacts and plugins'
upload.group = 'publishing'
upload.dependsOn(uploadLibrary)
upload.dependsOn(publishPlugins)

def isNonStable = { String version ->
  boolean stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  String regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}
