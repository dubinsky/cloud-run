plugins {
  id 'com.github.ben-manes.versions' version '0.42.0'
  id 'scala'
  id 'java-library'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'signing'
  id 'com.gradle.plugin-publish' version '0.20.0'
}

description = 'Deploy Service to Google Cloud Run'
group = 'org.podval.tools'
version = '0.3.0'

final String gitHubRepository = "dubinsky/cloud-run"
final String gitHubRepositoryUrl = "https://github.com/$gitHubRepository"
final String orgName = 'Podval Group'
final String orgUrl = 'https://www.podval.org'
final List<String> projectTags = ['Google', 'CloudRun', 'JIB', 'Docker']

dependencies {
  implementation 'org.scala-lang:scala3-library_3:3.1.2-RC1'
  implementation 'com.google.auth:google-auth-library-oauth2-http:1.4.0'
  implementation 'com.google.api-client:google-api-client:1.33.2'
  implementation 'com.google.apis:google-api-services-run:v1-rev20211112-1.32.1'
  // TODO switch to V2: implementation 'com.google.apis:google-api-services-run:v2-rev20220128-1.32.1'
  implementation 'org.slf4j:slf4j-api:1.7.32'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.1'

  // by default, -android version gets dragged in and breaks JIB - I get java.lang.NoSuchMethodError
  //   com.google.common.collect.ImmutableList.toImmutableList()Ljava/util/stream/Collector;
  implementation 'com.google.guava:guava:31.0.1-jre'

  compileOnly    'gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:3.2.0'
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.with {
    additionalParameters = [
      '-new-syntax',
      '-feature',
      '-language:strictEquality',
      '-source:future',
      //'-deprecation',
      //'-unchecked',
      //'-Xsemanticdb',
      //'-Ysafe-init',
      //'-explain',
    ]
  }
}

jar {
  manifest {
    attributes(
      'Implementation-Title'  : project.description,
      'Implementation-Version': project.version
    )
  }
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  archiveClassifier.set('sources')
}

task scaladocJar(type: Jar) {
  from scaladoc.destinationDir
  archiveClassifier.set('scaladoc')
}
scaladocJar.dependsOn scaladoc

// There is no Java in the project :)
project.gradle.startParameter.excludedTaskNames.add('compileJava')
// but Maven Central requires javadoc JAR...
task javadocJar(type: Jar) {
  from javadoc.destinationDir
  archiveClassifier.set('javadoc')
}
javadocJar.dependsOn(javadoc)

publishing {
  repositories {
    maven {
      name = 'mavenCentral'
      url = version.endsWith('SNAPSHOT') ?
        'https://oss.sonatype.org/content/repositories/snapshots' :
        'https://oss.sonatype.org/service/local/staging/deploy/maven2'

      // Note: this will use mavenCentralUsername and mavenCentralPassword properties - if they are available
      credentials(PasswordCredentials)
    }
  }

  publications {
    library(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact scaladocJar
      artifact javadocJar

      pom {
        name = project.name
        description = project.description
        url = gitHubRepositoryUrl
        scm {
          url = gitHubRepositoryUrl
          connection = "scm:git:git://github.com/${gitHubRepository}.git"
          developerConnection = "scm:git:ssh://github.com/${gitHubRepository}.git"
        }
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
            comments = 'A business-friendly OSS license'
          }
        }
        organization {
          name = orgName
          url = orgUrl
        }
        developers {
          developer {
            id = 'dub'
            name = 'Leonid Dubinsky'
            email = 'dub@podval.org'
            url = 'https://dub.podval.org'
            organization = orgName
            organizationUrl = orgUrl
            timezone = '-5'
          }
        }
      }
    }
  }
}

signing {
  useInMemoryPgpKeys(findProperty('gnupg.dub-podval-org.key'), findProperty('gnupg.dub-podval-org.password'))
  sign publishing.publications.library
}

gradlePlugin {
  plugins {
    cloudRun {
      id = project.name
      implementationClass = 'org.podval.tools.cloudrun.CloudRunPlugin'
    }
  }
}

pluginBundle {
  website = gitHubRepositoryUrl
  vcsUrl  = gitHubRepositoryUrl //.git' ?

  plugins {
    cloudRun {
      displayName = project.description
      description = project.description
      tags        = projectTags
    }
  }

  // If you have an existing plugin deployed to Bintray and would like to keep
  // your existing group ID and artifact ID for continuity, you can specify
  // them here (needs manual approval on first publication).
  mavenCoordinates {
    groupId    = project.group
    artifactId = project.name
  }
}

task uploadLibrary
uploadLibrary.description('Upload artifacts')
uploadLibrary.group('publishing')
uploadLibrary.dependsOn(publishLibraryPublicationToMavenCentralRepository)

task upload
upload.description('Upload artifacts and plugins')
upload.group('publishing')
upload.dependsOn(uploadLibrary)
upload.dependsOn(publishPlugins)
